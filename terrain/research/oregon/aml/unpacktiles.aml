/* look for all .asc files in input dir
/* load them and split into 1 degree tiles

&if %:program%_ eq ARC_ &then grid
&if %:program%_ ne GRID_ &then
&return This program must be run from ARC or GRID.

/*note the directory path
&sv indir I:\NCEAS\topo\incoming\srtmv41
&sv tiledir I:\NCEAS\topo\tiles
&sv name srtmv41
&sv vernum 1


&do x = 1 &to 72
  &if %x% > 9 &then &sv xn %x%; &else &sv xn 0%x%
  &do y = 1 &to 36
    &if %y% > 9 &then &sv yn %y%; &else &sv yn 0%y%
    &sv ascfile %indir%\srtm_%xn%_%yn%.asc
    &if [exists %ascfile% -file] &then
    &do
      &type Got %ascfile%
      
      /* import the ASCII file 
      &sv tmpgrd [scratchname -dir -full]
      /* &sv tmpgrd c:\workspace\xx00000 - was using for testing
      /* &type Using %tmpgrd% as temporary grid
      %tmpgrd% = asciigrid(%ascfile%)
      
      /* determine x (long) and y (lat) range
      &describe %tmpgrd%
      &sv xmin %grd$xmin%
      &sv ymin %grd$ymin%
      &sv xmax %xmin% + 5
      &sv ymax %ymin% + 5
      &sv cell %grd$dx%
      
      &type %xmin% %ymin% %xmax% %ymax%
      
      &do x = %xmin% &to %xmax%
        &if %x% < 0 &then &sv xtile w[round [abs %x%]]; &else &sv xtile e[round %x%]
        &sv x1 %x% + 1
        &do y = %ymin% &to %ymax%
          &if %y% < 0 &then &sv ytile s[round [abs %y%]]; &else &sv ytile n[round %y%]
          &sv y1 %y% + 1
          setwindow %x% %y% %x1% %y1%
          &type Tile %x% %y%
          show setwindow
          &sv tmptile [scratchname -dir -full]
          &type Create tile %xtile%%ytile%
          %tmptile% = %tmpgrd%
          &describe %tmptile%
          &if %grd$stdv% > 0 &then &do
            /* grid is not all nodata
            &sv dirname %tiledir%\%xtile%
            &if ^ [exists %dirname% -directory] &then &sys mkdir %dirname%
            &sv dirname %tiledir%\%xtile%\%ytile%
            &if ^ [exists %dirname% -directory] &then &sys mkdir %dirname%
            &sv dirname %tiledir%\%xtile%\%ytile%\%vernum%
            &if ^ [exists %dirname% -directory] &then &sys mkdir %dirname%
            &sv dirname %tiledir%\%xtile%\%ytile%\%vernum%\%name%
            &if ^ [exists %dirname% -directory] &then &sys mkdir %dirname%
            &sv gridname %dirname%\%xtile%%ytile%
            &if [exists %gridname% -grid] &then &do
              &type WARNING: did not expect to find %gridname%!
            &end
            &else &do
              copy %tmptile% %dirname%\%xtile%%ytile%
            &end
          &end
          kill %tmptile%
        &end
      &end
    
      kill %tmpgrd%
      
    &end
  &end 
&end